// Collections and indexes
CreateCollection({ name: "users" })

CreateIndex({
  name: "users_by_username",
  source: Collection("users"),
  terms: [{field: ["data", "username"]}],
  unique: true,
  serialized: true,
})

CreateCollection({ name: "movies"});

// Indexes for sorting purposes
// sorting needs to be done db side incase of pagination
CreateIndex({
  name: "movies_by_user_sort_by_title_asc",
  source: Collection("movies"),
  terms: [{field:["data", "owner"]}],
  values: [
    { field: ["data", "title"] },
    { field: ["ref"] }
  ]
})

CreateIndex({
  name: "movies_by_user_sort_by_title_desc",
  source: Collection("movies"),
  terms: [{field:["data", "owner"]}],
  values: [
    { field: ["data", "title"], reverse: true },
    { field: ["ref"] }
  ]
})

CreateIndex({
  name: "movies_by_user_sort_by_score_asc",
  source: Collection("movies"),
  terms: [{field:["data", "owner"]}],
  values: [
    { field: ["data", "score"] },
    { field: ["ref"] }
  ]
})

CreateIndex({
  name: "movies_by_user_sort_by_score_desc",
  source: Collection("movies"),
  terms: [{field:["data", "owner"]}],
  values: [
    { field: ["data", "score"], reverse: true },
    { field: ["ref"] }
  ]
})

CreateIndex({
  name: "movies_by_user_sort_by_year_asc",
  source: Collection("movies"),
  terms: [{field:["data", "owner"]}],
  values: [
    { field: ["data", "year"] },
    { field: ["ref"] }
  ]
})

CreateIndex({
  name: "movies_by_user_sort_by_year_desc",
  source: Collection("movies"),
  terms: [{field:["data", "owner"]}],
  values: [
    { field: ["data", "year"], reverse: true },
    { field: ["ref"] }
  ]
})

// Makes addEntry udf throw if we add the same entry to a list.
CreateIndex({
  name: "constraint",
  source: Collection("movies"),
  unique: true,
  serialized: true,
  terms: [
    {
      field: ["data", "owner"]
    },
    {
      field: ["data", "imdbId"]
    }
  ]
})

// UDFS
// user access controls are done with these.
// user has permissions to call them but has no other permissions in the db

// Add a list entry
// called as user with server permissions.
CreateFunction({
  name: "addEntry",
  role: "server",
  body: Query(
    Lambda(
      ["title", "score", "year", "rating", "note", "poster", "imdbId"],
      Create(Collection("movies"), {
        data: {
          title: Var("title"),
          score: ToInteger(Var("score")),
          year: Var("year"),
          rating: Var("rating"),
          note: Var("note"),
          poster: Var("poster"),
          imdbId: Var("imdbId"),
          added: ToString(Now()), // format: 2019-10-02T19:34:56.789012Z
          owner: Identity() // ref to adder.
        }
      })
    )
  )
})

// Get users movies, by username.
// Called by server, can't be called by user.
// takes username and index as paramas
CreateFunction({
  name: "getList",
  role: "server",
  body: Query(
  Lambda(
    ["username", "index"],
    Let(
      {
        user: Get(Match(Index("users_by_username"), Var("username"))),
        userRef: Select("ref", Var("user"))
      },
      Map(
        Paginate(Match(Index(Var("index")), Var("userRef")), {
          size: 100
        }),
        Lambda(
          ["first","ref"],
          Let(
            { data: Select("data", Get(Var("ref"))) },
            {
              title: Select("title", Var("data")),
              score: Select("score", Var("data")),
              year: Select("year", Var("data")),
              rating: Select("rating", Var("data")),
              note: Select("note", Var("data")),
              poster: Select("poster", Var("data")),
              imdbId: Select("imdbId", Var("data")),
              added: Select("added", Var("data")),
              id: Select("id", Var("ref"))
            }
          )
        )
      )
    )
  )
)
})

// Remove list entry.
CreateFunction({
  name: "deleteEntry",
  role: "server",
  body: Query(
  Lambda(
    "movieId",
    Let(
      {
        movieRef: Ref(Collection("movies"), Var("movieId")),
        movieData: Select("data", Get(Var("movieRef"))),
        movieOwnerRef: Select("owner", Var("movieData"))
      },
      If(
        Equals(Var("movieOwnerRef"), CurrentIdentity()),
        Delete(Var("movieRef")),
        Abort("Not your entry")
      )
    )
  )
)
})


// Update list entry score
CreateFunction({
  name: "updateScore",
  role: "server",
  body: Query(
  Lambda(
    ["score", "movieId"],
    Let(
      {
        movieRef: Ref(Collection("movies"), Var("movieId")),
        movieData: Select("data", Get(Var("movieRef"))),
        movieOwnerRef: Select("owner", Var("movieData"))
      },
      If(
        Equals(Var("movieOwnerRef"), CurrentIdentity()),
        Update(Var("movieRef"), { data: { score: Var("score") } }),
        Abort("Not your entry")
      )
    )
  )
)
})

// Update notes
CreateFunction({
  name: "updateNotes",
  role: "server",
  body: Query(
  Lambda(
    ["note", "movieId"],
    Let(
      {
        movieRef: Ref(Collection("movies"), Var("movieId")),
        movieData: Select("data", Get(Var("movieRef"))),
        movieOwnerRef: Select("owner", Var("movieData"))
      },
      If(
        Equals(Var("movieOwnerRef"), CurrentIdentity()),
        Update(Var("movieRef"), { data: { note: Var("note") } }),
        Abort("Not your entry")
      )
    )
  )
)
})